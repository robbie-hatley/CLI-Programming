#!/usr/bin/perl

=pod

Euler-061_Cyclical-Figurate-Numbers.pl

Triangle, square, pentagonal, hexagonal, heptagonal, and
octagonal numbers are all figurate (polygonal) numbers
and are generated by the following formulae:
Triangle    P3,n=n(n+1)/2   1, 3,  6, 10, 15, ...
Square      P4,n=n^2        1, 4,  9, 16, 25, ...
Pentagonal  P5,n=n(3n-1)/2  1, 5, 12, 22, 35, ...
Hexagonal   P6,n=n(2n-1)    1, 6, 15, 28, 45, ...
Heptagonal  P7,n=n(5n-3)/2  1, 7, 18, 34, 55, ...
Octagonal   P8,n=n(3n-2)    1, 8, 21, 40, 65, ...

The ordered set of three 4-digit numbers: 8128, 2882, 8281,
has three interesting properties.

   1. The set is cyclic, in that the last two digits of each
      number is the first two digits of the next number
      (including the last number with the first).

   2. Each polygonal type: triangle (P3,127=8128), square
      (P4,91=8281), and pentagonal (P5,44=2882), is represented
      by a different number in the set.

   3. This is the only set of 4-digit numbers with this property.

Find the sum of the only ordered set of six cyclic 4-digit
numbers for which each polygonal type: triangle, square,
pentagonal, hexagonal, heptagonal, and octagonal, is
represented by a different number in the set.

=cut

use v5.38;
use strict;
use warnings;
use Math::Combinatorics;

# Subroutines for generating triangle, square, pentagon, hexagon, heptagon, octagon numbers:
sub P3 ($n) { return $n*($n+1)/2   }
sub P4 ($n) { return $n*$n         }
sub P5 ($n) { return $n*(3*$n-1)/2 }
sub P6 ($n) { return $n*(2*$n-1)   }
sub P7 ($n) { return $n*(5*$n-3)/2 }
sub P8 ($n) { return $n*(3*$n-2)   }

# Subroutines for extracting the first 2 or last 2 digits of a 4-digit positive integer:
sub first_two ($x) {return int $x / 100}
sub last_two  ($x) {return     $x % 100}

# Make arrays of triangle, square, pentagon, hexagon, heptagon, octagon numbers:
my @Numbers = ([],[],[],[],[],[]);

# Fill arrays of triangle, square, pentagon, hexagon, heptagon, octagon numbers:
for ( my $i = 1 ; $i <= 150 ; ++$i ) {
   my $p3 = P3($i); $p3 =~ m/^[1-9]\d[1-9]\d$/ and push @{$Numbers[0]}, $p3;
   my $p4 = P4($i); $p4 =~ m/^[1-9]\d[1-9]\d$/ and push @{$Numbers[1]}, $p4;
   my $p5 = P5($i); $p5 =~ m/^[1-9]\d[1-9]\d$/ and push @{$Numbers[2]}, $p5;
   my $p6 = P6($i); $p6 =~ m/^[1-9]\d[1-9]\d$/ and push @{$Numbers[3]}, $p6;
   my $p7 = P7($i); $p7 =~ m/^[1-9]\d[1-9]\d$/ and push @{$Numbers[4]}, $p7;
   my $p8 = P8($i); $p8 =~ m/^[1-9]\d[1-9]\d$/ and push @{$Numbers[5]}, $p8;
}

=pod

say '';
say 'List of 4-digit triangle numbers:';
say $_ for @{$Numbers[0]};

say '';
say 'List of 4-digit square numbers:';
say $_ for @{$Numbers[1]};

say '';
say 'List of 4-digit pentagon numbers:';
say $_ for @{$Numbers[2]};

say '';
say 'List of 4-digit hexagon numbers:';
say $_ for @{$Numbers[3]};

say '';
say 'List of 4-digit heptagon numbers:';
say $_ for @{$Numbers[4]};

say '';
say 'List of 4-digit octagon numbers:';
say $_ for @{$Numbers[5]};

=cut

my @shapes = ('triangle','square','pentagon','hexagon','heptagon','octagon');
my @shapesets = permute(1,2,3,4,5);
for my $shapeset (@shapesets) {unshift @$shapeset, 0}
for my $shapeset (@shapesets) {
   # say 'Current shapeset = (', join(', ', @$shapeset), ')';
   for my $p0 (@{$Numbers[$$shapeset[0]]}) {
      for my $p1 (@{$Numbers[$$shapeset[1]]}) {
         next if last_two($p0) != first_two($p1);
         for my $p2 (@{$Numbers[$$shapeset[2]]}) {
            next if last_two($p1) != first_two($p2);
            for my $p3 (@{$Numbers[$$shapeset[3]]}) {
               next if last_two($p2) != first_two($p3);
               for my $p4 (@{$Numbers[$$shapeset[4]]}) {
                  next if last_two($p3) != first_two($p4);
                  for my $p5 (@{$Numbers[$$shapeset[5]]}) {
                     next if last_two($p4) != first_two($p5);
                     next if last_two($p5) != first_two($p0);
                     my @o = @shapes[@$shapeset];
                     say "FOUND CYCLIC ORDERED SET: "
                       . "$p0 ($o[0]), $p1 ($o[1]), $p2 ($o[2]), "
                       . "$p3 ($o[3]), $p4 ($o[4]), $p5 ($o[5])  ";
                     say 'Sum = ', $p0+$p1+$p2+$p3+$p4+$p5;
                  }
               }
            }
         }
      }
   }
}
